<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:salary="com.gc.hr.view.salary.*"
         width="100%" height="100%" preinitialize="onPreinitialize()">
  <mx:AdvancedDataGrid id="adg3" dataProvider="{list1}" width="100%" height="100%" editable="false"
                       horizontalScrollPolicy="auto" lockedColumnCount="5" sortExpertMode="true"
                       labelFunction="{CommonUtil.gridLabelFunction}"
                       creationComplete="onCreationComplete()">
    <mx:columns>
      <mx:AdvancedDataGridColumn dataField="{Constants.PROP_NAME_SERIAL}" width="64"
                                 labelFunction="{CommonUtil.serialLabelFunction}"
                                 styleFunction="{CommonUtil.serialStyleFunction}"/>
      <mx:AdvancedDataGridColumn dataField="selected"
                                 headerText="{resourceManager.getString('gcc', 'select')}"
                                 textAlign="center">
        <mx:itemRenderer>
          <mx:Component>
            <mx:CheckBox click="data.selected=!data.selected" selected="{data.selected}"/>
          </mx:Component>
        </mx:itemRenderer>
      </mx:AdvancedDataGridColumn>
      <mx:AdvancedDataGridColumn dataField="depart" dataTipField="depart.name"/>
      <mx:AdvancedDataGridColumn dataField="office"/>
      <!--mx:AdvancedDataGridColumn dataField="lineName#pinyin" dataTipField="lineName"/>
           <mx:AdvancedDataGridColumn dataTipField="busUseId"/-->
      <mx:AdvancedDataGridColumn dataField="workerId"/>
      <mx:AdvancedDataGridColumn dataField="name#pinyin" dataTipField="name"/>
      <mx:AdvancedDataGridColumn dataField="onDate"/>
      <mx:AdvancedDataGridColumn dataField="downDate"/>
      <mx:AdvancedDataGridColumn dataField="allotDate"/>
      <mx:AdvancedDataGridColumn dataField="type"/>
      <mx:AdvancedDataGridColumn dataField="grade"/>
      <mx:AdvancedDataGridColumn dataField="fkPosition" dataTipField="fkPosition.name"/>
      <mx:AdvancedDataGridColumn dataField="upgradeDate"/>
      <mx:AdvancedDataGridColumn width="1" sortable="false"/>
    </mx:columns>
  </mx:AdvancedDataGrid>
  <mx:ToolBar width="100%" paddingTop="6" paddingLeft="6" paddingBottom="6">
    <mx:FormItem label="{resourceManager.getString('gcc', 'person.depart.name')}:">
      <mx:ComboBox id="cbDepart" width="120" itemRenderer="mx.controls.Label"
                   change="onDepartChange(event)"/>
    </mx:FormItem>
    <mx:FormItem label="{resourceManager.getString('gcc', 'person.fkPosition.name')}:">
      <mx:ComboBox id="cbPosition" width="120" itemRenderer="mx.controls.Label"
                   change="onPositionChange(event)"/>
    </mx:FormItem>
    <mx:FormItem label="{resourceManager.getString('gcc', 'search')}:">
      <mx:TextInput id="tiSearch" width="120" change="onSearchChange(event)"/>
    </mx:FormItem>
    <mx:PopUpButton id="pub0" label="{resourceManager.getString('gcc', 'person.downStatus')}"/>
    <mx:PopUpButton id="pub1" label="{resourceManager.getString('gcc', 'select')}"/>
    <mx:Button label="{resourceManager.getString('gcc', 'refresh')}" click="onRefresh(event)"/>
    <mx:Button label="{resourceManager.getString('gcc', 'export')}" click="onExport(event)"/>
    <mx:Button label="{resourceManager.getString('gcc_hr', 'salDeptPsn.assign')}"
               click="assign(event)" enabled="{access}"/>
    <mx:Button label="{resourceManager.getString('gcc', 'close')}" click="close(event)"/>
  </mx:ToolBar>
  <mx:Script>
    <![CDATA[
      import mx.collections.ArrayCollection;
      import mx.controls.Alert;
      import mx.events.MenuEvent;
      import mx.rpc.events.ResultEvent;
      import mx.utils.ObjectUtil;

      import com.gc.Constants;
      import com.gc.CommonEvent;
      import com.gc.common.model.Department;
      import com.gc.common.model.Person;
      import com.gc.common.model.Position;
      import com.gc.common.controller.BaseController;
      import com.gc.common.controller.UserController;
      import com.gc.hr.controller.PersonalController;
      import com.gc.hr.model.SalDeptPsn;
      import com.gc.hr.Events;
      import com.gc.hr.controller.SalaryController;
      import com.gc.util.CommonUtil;
      import com.gc.util.HanZiUtil;

      [Bindable]
      private var checkBox:ClassFactory;
      [Bindable]
      private var access:Boolean;
      private var list0:ArrayCollection;
      [Bindable]
      private var list1:ArrayCollection;
      private var downStatus:Object={no:true, yes:false};

      private function onPreinitialize():void
      {
        access=UserController.hrHead && UserController.hrSalWrite;
        this.label=resourceManager.getString('gcc_hr', 'salDeptPsn.page3');
        addEventListener(Events.ADD_SALDEPTPSN, onAddSalDeptPsn);
        addEventListener(Events.DEL_SALDEPTPSN, onDelSalDeptPsn);
        addEventListener(CommonEvent.CHANGED, onChangeHandler);
      }

      private function onCreationComplete():void
      {
        CommonUtil.setDataGridColumns(adg3, "gcc", "person");
        adg3.width=this.width;
        var departs:ArrayCollection=ObjectUtil.copy(BaseController.departmentList) as ArrayCollection;
        departs.addItemAt(new Department(0, UserController.branch, resourceManager.getString("gcc", "depart.all")), 0);
        cbDepart.dataProvider=departs;
        cbDepart.selectedIndex=-1;
        var positions:ArrayCollection=ObjectUtil.copy(PersonalController.positionList) as ArrayCollection;
        positions.addItemAt(new Position(UserController.branch, null, resourceManager.getString("gcc", "position.all")), 0);
        cbPosition.dataProvider=positions;
        cbPosition.selectedIndex=-1;
        if (UserController.hrSub)
        {
          cbDepart.enabled=false;
          cbDepart.selectedIndex=CommonUtil.indexOfKey(departs, UserController.hrDepart);
        }
        var menu:Object={
            "downStatus":[{label:resourceManager.getString('gcc', 'person.downStatus.no'), type:"check", id:"no"},
              {label:resourceManager.getString('gcc', 'person.downStatus.yes'), type:"check", id:"yes"},],
            "select":[{label:resourceManager.getString("gcc", "select.all"), click:selectAll},
              {label:resourceManager.getString("gcc", "select.none"), click:selectNone},
              {label:resourceManager.getString("gcc", "select.reverse"), click:selectReverse},]
          };
        for each (var item:Object in menu.downStatus)
          item.toggled=getDownStatus(item);
        CommonUtil.createPopUpMenu(pub0, menu.downStatus, filter);
        CommonUtil.createPopUpMenu(pub1, menu.select);
        load();
      }

      private function getDownStatus(obj:Object):Boolean
      {
        return obj == null ? false : downStatus[obj.id];
      }

      private function setDownStatus(obj:Object, item:Object):void
      {
        if (obj == null || item == null)
          return;
        downStatus[obj.id]=item.toggled;
      }

      private function onDepartChange(e:Event):void
      {
        filter();
      }

      private function onPositionChange(e:Event):void
      {
        filter();
      }

      private function onSearchChange(e:Event):void
      {
        filter();
      }

      private function selectAll(e:Event, obj:Object=null):void
      {
        list1.source.forEach(function(item:*, index:int, arr:Array):void{item.selected=true;});
      }

      private function selectNone(e:Event, obj:Object=null):void
      {
        list1.source.forEach(function(item:*, index:int, arr:Array):void{item.selected=false;});
      }

      private function selectReverse(e:Event, obj:Object=null):void
      {
        list1.source.forEach(function(item:*, index:int, arr:Array):void{item.selected=!item.selected;});
      }

      private function onRefresh(e:Event):void
      {
        load();
      }

      private function onExport(e:Event):void
      {
        CommonUtil.export(CommonUtil.getGridData(adg3), CommonUtil.getGridHeaders(adg3), null, 
          resourceManager.getString("gcc_hr", "salDeptPsn.page3.filename", [UserController.branchName]));
      }

      private function assign(e:Event):void
      {
        var persons:Array=list1.source.filter(function(item:Object, idx:uint, arr:Array):Boolean{return item.selected;});
        if (persons.length <= 0)
        {
          Alert.show(resourceManager.getString("gcc_hr", "salDeptPsn.not.null"), 
            Constants.APP_NAME, Alert.OK, null, null, Constants.ICON32_WARNING);
          return;
        }
        CommonUtil.popupWindow(DeptPsnAssign, "", this, persons);
      }

      private function onAddSalDeptPsn(e:Events):void
      {
        if (list0)
        {
          for (var i:int=list0.length-1; i >= 0; i--)
          {
            if (list0.getItemAt(i).selected)
              list0.removeItemAt(i);
          }
          filter();
        }
        if (!e.data)
          e.data=3;
        else
          e.stopImmediatePropagation();
      }

      private function onDelSalDeptPsn(e:Events):void
      {
        e.stopImmediatePropagation();
        if (list0 && e.data==2)
        {
          list0=null;
        }
      }

      private function onChangeHandler(e:CommonEvent):void
      {
        e.stopImmediatePropagation();
        if (list0==null)
          load();
      }

      private function load():void
      {
        if (list0 != null)
          list0.removeAll();
        SalaryController.getDeptPsnListC({"branch.id": UserController.branchId}, function(e:ResultEvent):void
          {
            list0=e.result as ArrayCollection;
            for each (var obj:Object in list0)
            {
              obj["name#pinyin"]=HanZiUtil.pinyin(obj.name);
              obj["lineName#pinyin"]=HanZiUtil.pinyin(obj.lineName);
            }
            filter();
          });
      }

      private function filter(e:MenuEvent=null, obj:Object=null):void
      {
        setDownStatus(obj, e ? e.item : null);
        var fno:Boolean=downStatus.no;
        var fyes:Boolean=downStatus.yes;
        var d:Department=cbDepart.selectedItem as Department;
        var did:int=d ? d.id : 0;
        var p:Position=cbPosition.selectedItem as Position;
        var pno:String=p ? p.id.no : null;
        var ss:String=CommonUtil.trim(tiSearch.text);
        list1=new ArrayCollection(list0.source.filter(function(item:Object, idx:uint, arr:Array):Boolean
          {
            return ((fno && item.isOn) || (fyes && item.isDown)) 
              && (did == 0 || item.departId == did)
              && (pno == null || item.position == pno)
              && (ss == "" || item.name.indexOf(ss) >= 0 || item.workerId.indexOf(ss) >= 0);
          }));
      }

      private function close(e:Event):void
      {
        dispatchEvent(CommonEvent.CLOSE_EVENT);
      }
    ]]>
  </mx:Script>
</mx:VBox>
